generateJavaFromXsd {
	inputs.file file("${xsdDir}/clearth.xsd")
	inputs.file file("${xsdDir}/clearth.xjb")
	inputs.file file("${xsdDir}/flatdictionary.xsd")

	outputs.files fileTree("${genJavaDir}/com/exactprosystems/clearth/xmldata").include('*.java')
	outputs.files fileTree("${genJavaDir}/com/exactprosystems/clearth/connectivity/flat").include('*.java')

	doLast {
		ant.taskdef(name: 'xjc', classname: 'com.sun.tools.xjc.XJCTask', classpath: configurations.jaxb.asPath)

	ant.xjc(schema: "${xsdDir}/clearth.xsd", binding: "${xsdDir}/clearth.xjb",
				destdir: "${genJavaDir}", package: 'com.exactprosystems.clearth.xmldata',
				removeOldOutput: 'yes')
		{
			produces(dir: "${genJavaDir}/com/exactprosystems/clearth/xmldata", includes: '*')
		}

	ant.xjc(schema: "${xsdDir}/message.xsd", binding: "${xsdDir}/message.xjb",
			destdir: "${genJavaDir}", package: 'com.exactprosystems.clearth.connectivity.message',
			removeOldOutput: 'yes')
		{
			arg(value: "-episode")
			arg(value: "${genDir}/message.episode")
			produces(dir: "${genJavaDir}/com/exactprosystems/clearth/connectivity/message", includes: '*')
		}

		ant.xjc(schema: "${xsdDir}/flatdictionary.xsd",
				destdir: "${genJavaDir}", package: 'com.exactprosystems.clearth.connectivity.flat',
				removeOldOutput: 'yes', extension: true)
		{
			arg(value: "-catalog")
			arg(value: "${coreXsdDir}/message.cat")
			arg(value: "-b")
			arg(value: "${genDir}/message.episode")
			produces(dir: "${genJavaDir}/com/exactprosystems/clearth/connectivity/flat", includes: '*')
		}
	}
}

dependencies {
	def poiVersion = '5.2.5'
	def xmlunitVersion = '2.10.0'
	
	api "commons-beanutils:commons-beanutils:1.9.4"
	api "commons-codec:commons-codec:1.16.0"
	api "commons-fileupload:commons-fileupload:1.5"
	api "commons-io:commons-io:2.15.1"
	api "commons-lang:commons-lang:2.6"
	api "commons-logging:commons-logging:1.2"
	api "org.apache.commons:commons-collections4:4.4"
	api "org.apache.commons:commons-lang3:3.14.0"
	api "org.apache.commons:commons-csv:1.5"
	api "org.apache.commons:commons-exec:1.3"
	api "org.apache.commons:commons-text:1.11.0"
	api "org.apache.commons:commons-compress:1.26.0"  //Newer version of library than used by org.apache.poi:poi:5.2.5
	api "org.apache.poi:poi:${poiVersion}"
	api "org.apache.poi:poi-ooxml:${poiVersion}"
	api "org.apache.poi:poi-ooxml-lite:${poiVersion}"
	api "org.freemarker:freemarker:2.3.30"
	api "com.fasterxml.jackson.core:jackson-databind:2.16.1"
	api "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.16.1"
	api "javax.xml.bind:jaxb-api:2.3.0"
	api "com.sun.xml.bind:jaxb-impl:2.3.9"
	api "org.mvel:mvel2:2.4.0.Final"
	api "org.xmlunit:xmlunit-core:${xmlunitVersion}"
	api ("org.xmlunit:xmlunit-legacy:${xmlunitVersion}") {
		exclude (group: 'junit', module: 'junit')
	}
	api "org.xmlunit:xmlunit-matchers:${xmlunitVersion}"
	api "com.thoughtworks.xstream:xstream:1.4.20"
	api "javax.annotation:javax.annotation-api:1.3.2"
	api "javax.jms:javax.jms-api:2.0.1"
	
	api "org.slf4j:slf4j-api:1.7.36"
	api "org.slf4j:jul-to-slf4j:1.7.36"
	api "org.apache.logging.log4j:log4j-core:2.21.1"
	api "org.apache.logging.log4j:log4j-slf4j-impl:2.21.1"
	api "org.apache.logging.log4j:log4j-jcl:2.21.1"
	
	api "javax.servlet:javax.servlet-api:3.1.0"
	api "javax.activation:activation:1.1"
}

jar {
	manifest {
		attributes("Specification-Title": "ClearTH Core")
	}
}

publishing {
	publications {
		core(MavenPublication) {
			groupId = "${group}"
			artifactId = 'clearth-core'
			version = "${version}"
			
			from components.java
		}
	}
}

artifactoryPublish {
	publications(publishing.publications.core)
}