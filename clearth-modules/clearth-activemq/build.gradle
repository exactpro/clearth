dependencies {
    def v = "5.18.3"
    implementation "org.apache.activemq:activemq-broker:${v}"
    implementation "org.apache.activemq:activemq-kahadb-store:${v}"
}

publishing {
    publications {
        activeMq(MavenPublication) {
            groupId = "${group}"
            artifactId = 'clearth-activemq'
            version = "${version}"

            from components.java
        }
    }
}

configurations {
    all {
        exclude group: 'org.apache.activemq', module: 'activemq-openwire-legacy'
        exclude group: 'org.apache.activemq.protobuf', module: 'activemq-protobuf'
        exclude group: 'org.slf4j', module: 'slf4j-api'
    }
}

ext {
    moduleName = 'ActiveMQ'
    moduleImpl = 'com.exactprosystems.clearth:clearth-activemq'
    if (!project.hasProperty('clearthDir'))
        clearthDir = undef
}

artifactoryPublish {
    publications(publishing.publications.activeMq)
}

task installModule {
    doLast {
        def type = new Tuple4("ActiveMQ",
                "com.exactprosystems.clearth.connectivity.jms.activemq.ActiveMqConnection",
                "connections/activemq",
                ["com.exactprosystems.clearth.connectivity.jms.activemq.ActiveMqReadQNotReadByOthersRule"])
        installConTypeModule(clearthDir, moduleName, moduleImpl, type)
    }
}